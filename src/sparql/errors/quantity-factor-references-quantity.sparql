PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX vim4:  <http://bipm.org/jcgm/vim4#>
PREFIX oml: <http://opencaesar.io/oml#>

# Without bundle closure, an OWL2-DL reasoner allows 
# an instance, ?q, to be classified as both 
# vim4:SystemHasUnitaryQuantity and vim4:GeneralUnitaryQuantity.
# This happens when the factor of a derived quantity is mistakenly
# a quantity instead of the membership of that quantity in a system of quantities.
# That is, the ill-formed derived quantity factor pattern looks like this:
#
# ?qf rdf:type vim4:SystemHasDerivedUnitaryQuantity 
# ?qf oml:hasTarget ?q
# ?q rdf:type vim4:SystemHasUnitaryQuantity 
# ?q rdf:type vim4:GeneralUnitaryQuantity 
#
# OML bundle closure forces vim4:SystemHasUnitaryQuantity and vim4:GeneralUnitaryQuantity 
# to be disjoint. This makes the above pattern logically inconsistent since ?q
# cannot belong to a type and its complement.

SELECT ?qf ?q
WHERE {
  ?qf rdf:type vim4:HasPowerOfUnitaryQuantityFactor ;
      oml:hasTarget ?q .
  ?q rdf:type vim4:SystemHasUnitaryQuantity ;
     rdf:type vim4:GeneralUnitaryQuantity .
}