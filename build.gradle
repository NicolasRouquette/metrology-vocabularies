
/* 
 * The Gradle task dependencies 
 */
buildscript {
	repositories {
  	mavenLocal()
    mavenCentral()
  }
  dependencies {
		// https://github.com/opencaesar/owl-adapter/tags
		classpath 'io.opencaesar.adapters:oml2owl-gradle:1.0.+'
		
		// https://github.com/opencaesar/oml-tools/tags
    classpath 'io.opencaesar.oml:oml-merge-gradle:1.0.+'
		classpath 'io.opencaesar.oml:oml-bikeshed-gradle:1.0.+'
		
		// https://github.com/opencaesar/owl-tools/tags
		classpath 'io.opencaesar.owl:owl-load-gradle:+'
		classpath 'io.opencaesar.owl:owl-reason-gradle:+'
		classpath 'io.opencaesar.owl:owl-fuseki:+'
		classpath 'io.opencaesar.owl:owl-fuseki-gradle:+'
		classpath 'io.opencaesar.owl:owl-query-gradle:+'
  }
}

plugins {
  id 'com.palantir.git-version' version '0.12.3'
}

apply from: "${rootDir}/gradle/maven-deployment.gradle"

/* 
 * Dataset maven coordinates
 */
ext.title = 'Metrology Vocabularies'
description ='Metrology vocabularies from iupac.org, iso/iec iso-80000 and NIST Special publication 811'
group = 'io.opencaesar.ontologies'
version = gitVersion()

/*
 * The repositories to look up OML dependencies in
 */
repositories {
    mavenLocal()
    mavenCentral()
}

/*
 * The configuration of OML dependencies
 */
configurations {
    oml
}

/*
 * Dependency versions
 */
ext { 
    coreVersion = '3.+'
}

/*
 * The OML dependencies
 */
dependencies {
    oml "io.opencaesar.ontologies:core-vocabularies:$coreVersion"
}

/*
 * A task to extract and merge the OML dependencies
 */
task downloadDependencies(type:io.opencaesar.oml.merge.OmlMergeTask, group:"oml") {
    inputZipPaths = configurations.oml.files
    outputCatalogFolder = file('build/oml')
}

/*
 * A task to generate Bikeshed specification for the OML catalog
 */
task omlToBikeshed(type: io.opencaesar.oml.bikeshed.Oml2BikeshedTask, group:"oml", dependsOn: downloadDependencies) {
    // OML catalog
    inputCatalogPath = file('catalog.xml')
    // OML catalog title
    inputCatalogTitle = project.title
    // OML catalog version
    inputCatalogVersion = project.version
    // OWL folder
    outputFolderPath = file('build/bikeshed')
    // Publish URL
    publishUrl = "$dataset_publishUrl".toString()
}

/*
 * A task to generate the model documentation in HTML
 */
import org.gradle.internal.os.OperatingSystem
task generateDocs(dependsOn: omlToBikeshed, group:"oml") {
    doLast {
        if (OperatingSystem.current().isWindows()) {
            exec { 
                if (project.hasProperty('BIKESHED')) {
                    environment 'PATH', "${environment.PATH}:${BIKESHED}"
                }
                commandLine 'build/bikeshed/publish.bat' 
            }
        } else {
            exec { 
                commandLine 'chmod', '+x', 'build/bikeshed/publish.sh' 
            }
            exec {
                if (project.hasProperty('BIKESHED')) {
                    environment 'PATH', "${environment.PATH}:${BIKESHED}"
                }
                commandLine 'build/bikeshed/publish.sh'
            }
        }
    }
}

/*
 * A task to convert the OML catalog to OWL catalog
 */
task omlToOwl(type:io.opencaesar.oml2owl.Oml2OwlTask, group:"oml", dependsOn: downloadDependencies) {
    // OML catalog
    inputCatalogPath = file('catalog.xml')
    // OWL catalog
    outputCatalogPath = file('build/owl/catalog.xml')
}

/*
 * A task to run the Openllet reasoner on the OWL catalog
 */
task owlReasonMetrology(type:io.opencaesar.owl.reason.OwlReasonTask, dependsOn: omlToOwl) {
	// OWL catalog
	catalogPath = file('build/owl/catalog.xml')
	// Input ontology IRI to reason on
	inputOntologyIri = 'http://iupac.org/metrology-bundle'
	// Entailment statements to generate and the ontologies to persist them in
	specs = [
		'http://iupac.org/metrology-bundle/classes = ALL_SUBCLASS',
		'http://iupac.org/metrology-bundle/properties = INVERSE_PROPERTY | ALL_SUBPROPERTY',
		'http://iupac.org/metrology-bundle/individuals = ALL_INSTANCE | DATA_PROPERTY_VALUE | OBJECT_PROPERTY_VALUE | SAME_AS'
	]
	// Junit error report
	reportPath = file('build/reports/metrology-bundle/reasoning.xml')
}

task owlReasonIso80000(type:io.opencaesar.owl.reason.OwlReasonTask, dependsOn: omlToOwl) {
	// OWL catalog
	catalogPath = file('build/owl/catalog.xml')
	// Input ontology IRI to reason on
	inputOntologyIri = 'http://iso.org/iso-80000-bundle'
	// Entailment statements to generate and the ontologies to persist them in
	specs = [
		'http://iso.org/iso-80000-bundle/classes = ALL_SUBCLASS',
		'http://iso.org/iso-80000-bundle/properties = INVERSE_PROPERTY | ALL_SUBPROPERTY',
		'http://iso.org/iso-80000-bundle/individuals = ALL_INSTANCE | DATA_PROPERTY_VALUE | OBJECT_PROPERTY_VALUE | SAME_AS'
	]
	// Junit error report
	reportPath = file('build/reports/iso-80000-bundle/reasoning.xml')
}

task owlReasonNistSP811(type:io.opencaesar.owl.reason.OwlReasonTask, dependsOn: omlToOwl) {
	// OWL catalog
	catalogPath = file('build/owl/catalog.xml')
	// Input ontology IRI to reason on
	inputOntologyIri = 'http://nist.gov/pml/special-publication-811-bundle'
	// Entailment statements to generate and the ontologies to persist them in
	specs = [
		'http://nist.gov/pml/special-publication-811-bundle/classes = ALL_SUBCLASS',
		'http://nist.gov/pml/special-publication-811-bundle/properties = INVERSE_PROPERTY | ALL_SUBPROPERTY',
		'http://nist.gov/pml/special-publication-811-bundle/individuals = ALL_INSTANCE | DATA_PROPERTY_VALUE | OBJECT_PROPERTY_VALUE | SAME_AS'
	]
	// Junit error report
	reportPath = file('build/reports/special-publication-811-bundle/reasoning.xml')
}


task owlReasonVIM4(type:io.opencaesar.owl.reason.OwlReasonTask, dependsOn: omlToOwl) {
  // OWL catalog
  catalogPath = file('build/owl/catalog.xml')
  // Input ontology IRI to reason on
  inputOntologyIri = 'http://bipm.org/jcgm/vim4-bundle'
  // Entailment statements to generate and the ontologies to persist them in
  specs = [
    'http://bipm.org/jcgm/vim4-bundle/classes = ALL_SUBCLASS',
    'http://bipm.org/jcgm/vim4-bundle/properties = INVERSE_PROPERTY | ALL_SUBPROPERTY',
    'http://bipm.org/jcgm/vim4-bundle/individuals = ALL_INSTANCE | DATA_PROPERTY_VALUE | OBJECT_PROPERTY_VALUE | SAME_AS'
  ]
  // Junit error report
  reportPath = file('build/reports/vim4-bundle/reasoning.xml')
}

/*
 * Start the headless Fuseki server
 */
task startFuseki(type: io.opencaesar.owl.fuseki.StartFusekiTask, group:"oml") {
    configurationPath = file('.fuseki.ttl')
    outputFolderPath = file('.fuseki')
}

/*
 * Stop the headless Fuseki server
 */
task stopFuseki(type: io.opencaesar.owl.fuseki.StopFusekiTask, group:"oml") {
    outputFolderPath = file('.fuseki')
}

/*
 * A task to load the metrology dataset to a Fuseki dataset endpoint
 */
task owlLoadMetrology(type:io.opencaesar.owl.load.OwlLoadTask, group:"oml", dependsOn: owlReasonMetrology) {
    catalogPath = file('build/owl/catalog.xml')
    endpointURL = "http://localhost:3030/$dataset_name".toString()
    fileExtensions = ['owl', 'ttl']
    iris = [
		'http://iupac.org/metrology-bundle/classes',
		'http://iupac.org/metrology-bundle/properties',
		'http://iupac.org/metrology-bundle/individuals',
    ]
}

/*
 * A task to load the iso80000 dataset to a Fuseki dataset endpoint
 */
task owlLoadIso80000(type:io.opencaesar.owl.load.OwlLoadTask, group:"oml", dependsOn: owlReasonIso80000) {
    catalogPath = file('build/owl/catalog.xml')
    endpointURL = "http://localhost:3030/$dataset_name".toString()
    fileExtensions = ['owl', 'ttl']
    iris = [
		'http://iso.org/iso-80000-bundle/classes',
		'http://iso.org/iso-80000-bundle/properties',
		'http://iso.org/iso-80000-bundle/individuals',
    ]
}

/*
 * A task to load the nistsp811 dataset to a Fuseki dataset endpoint
 */
task owlLoadNistSP811(type:io.opencaesar.owl.load.OwlLoadTask, group:"oml", dependsOn: owlReasonNistSP811) {
    catalogPath = file('build/owl/catalog.xml')
    endpointURL = "http://localhost:3030/$dataset_name".toString()
    fileExtensions = ['owl', 'ttl']
    iris = [
		'http://nist.gov/pml/special-publication-811-bundle/classes',
	  	'http://nist.gov/pml/special-publication-811-bundle/properties',
	  	'http://nist.gov/pml/special-publication-811-bundle/individuals'
    ]
}

/*
 * A task to run a set of SPARQL queries on a Fuseki dataset endpoint
 */
/*task owlQuery(type:io.opencaesar.owl.query.OwlQueryTask, group:"oml", dependsOn: owlLoad) {
    endpointURL = "http://localhost:3030/$dataset_name".toString()
    queryPath = file('src/sparql')
    resultPath = file('build/frames')
}*/

/*
 * A task to build the project, which executes several tasks together
 */
tasks.named('build') {
    group "oml"
    dependsOn owlReasonMetrology
    dependsOn owlReasonIso80000
    dependsOn owlReasonNistSP811
    dependsOn owlReasonVIM4
}

/*
 * A task to delete the build artifacts
 */
tasks.named('clean') {
    group "oml"
}

/*
 * Publish artifact to maven
 */
task omlZip(type: Zip, group:"oml") {
    from file('src/oml')
    include "**/*.oml"
    destinationDirectory = file('build/libs')
    archiveBaseName = project.name
    archiveVersion = project.version
}

publishing.publications.maven.artifact omlZip

/*
 * Integration with the Eclipse IDE
 */ 
apply plugin: 'eclipse'

eclipse {
    synchronizationTasks downloadDependencies
}